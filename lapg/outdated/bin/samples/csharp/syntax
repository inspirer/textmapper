##########################################################################
#   C# phrase structure grammar
#	REQUIRES: lapg 1.2.2+
#   grammar compiler: http://sourceforge.net/projects/lapg/
#
#	From: C# Language specification Final Draft - October 2002 (by ECMA TC39/TG2)
#   Cast ambiguity resolved

# TODO: unsafe code

.maxtoken    4096
.getsym      "chr = buff[l++];if( l == end ) fillb()"
.positioning "full"
.class       "parser"
.namespace   "CSharpProcessor"
.lang        "cs"
.lexemend    "on"

# Vocabulary

# Identificators

identifier: /@?[a-zA-Z_][A-Za-z_0-9]*/ -1

# literals

Licon:      /([1-9][0-9]*|0[0-7]*|0[xX][0-9a-fA-F]+)([uU](l|L|ll|LL)?|(l|L|ll|LL)[uU]?)?/

Lfcon:		/([0-9]*\.[0-9]+|[0-9]+\.)([eE][+-]?[0-9]+)?[flFL]?/
Lfcon:		/[0-9]+[eE][+-]?[0-9]+[flFL]?|[0-9]+[fF]/
Lfcon:		/0[xX]([0-9a-fA-F]*\.[0-9a-fA-F]+|[0-9a-fA-F]+\.?)[pP][+-]?[0-9]+[flFL]?/

Lscon:      /@("[^"]*")+/
Lscon:      /L?"([^"\\]|\\(['"?\\abfnrtv]|x[0-9a-fA-F]+|[0-7]([0-7][0-7]?)?))*"/
Lccon:		/L?'([^\n\\']|\\(['"?\\abfnrtv]|x[0-9a-fA-F]+|[0-7]([0-7][0-7]?)?))+'/

# keywords

Labstract:	/abstract/
Las:		/as/
Lbase:		/base/
Lbool:		/bool/
Lbreak:		/break/
Lbyte:		/byte/
Lcase:		/case/
Lcatch:		/catch/
Lchar:		/char/
Lchecked:	/checked/
Lclass:		/class/
Lconst:		/const/
Lcontinue:	/continue/
Ldecimal:	/decimal/
Ldefault:	/default/
Ldelegate:	/delegate/
Ldo:		/do/
Ldouble:	/double/
Lelse:		/else/
Lenum:		/enum/
Levent:		/event/
Lexplicit:	/explicit/
Lextern:	/extern/
Lfalse:		/false/
Lfinally:	/finally/
Lfixed:		/fixed/
Lfloat:		/float/
Lfor:		/for/
Lforeach:	/foreach/
Lgoto:		/goto/
Lif:		/if/
Limplicit:	/implicit/
Lin:		/in/
Lint:		/int/
Linterface:	/interface/
Linternal:	/internal/
Lis:		/is/
Llock:		/lock/
Llong:		/long/
Lnamespace:	/namespace/
Lnew:		/new/
Lnull:		/null/
Lobject:	/object/
Loperator:	/operator/
Lout:		/out/
Loverride:	/override/
Lparams:	/params/
Lprivate:	/private/
Lprotected:	/protected/
Lpublic:	/public/
Lreadonly:	/readonly/
Lref:		/ref/
Lreturn:	/return/
Lsbyte:		/sbyte/
Lsealed:	/sealed/
Lshort:		/short/
Lsizeof:	/sizeof/
Lstackalloc:/stackalloc/
Lstatic:	/static/
Lstring:	/string/
Lstruct:	/struct/
Lswitch:	/switch/
Lthis:		/this/
Lthrow:		/throw/
Ltrue:		/true/
Ltry:		/try/
Ltypeof:	/typeof/
Luint:		/uint/
Lulong:		/ulong/
Lunchecked:	/unchecked/
Lunsafe:	/unsafe/
Lushort:	/ushort/
Lusing:		/using/
Lvirtual:	/virtual/
Lvoid:		/void/
Lvolatile:	/volatile/
Lwhile:		/while/

# end of Keywords

'{':        /{/
'}':		/}/
'[':		/\[/
']':		/\]/
'(':		/\(/
')':		/\)/
'.':		/\./
',':		/,/
':':		/:/
';':		/;/
'+':		/+/
'-':		/-/
'*':		/*/
'/':		/\//
'%':		/%/
'&':		/&/
'|':		/\|/
'^':		/^/
'!':		/!/
'~':		/~/
'=':		/=/
'<':		/</
'>':		/>/
'?':		/?/
'++':		/\+\+/
'--':		/--/
'&&':		/&&/
'||':		/\|\|/
'<<':		/<</
'>>':		/>>/
'==':		/==/
'!=':		/!=/
'<=':		/<=/
'>=':		/>=/
'+=':		/+=/
'-=':		/-=/
'*=':		/*=/
'/=':		/\/=/
'%=':		/%=/
'&=':		/&=/
'|=':		/\|=/
'^=':		/^=/
'<<=':		/<<=/
'>>=':		/>>=/
'->':		/->/

# special token to resolve cast or call conflict

cast_token:

comment:	/\/\/\/.*/		\ continue;

_skip:      /\/\/([^\/\n\r].*)?/
_skip:		/#.*/
_skip:      /[\t\r\n ]+/    \ continue;

'/*':   /\/\*/          { group = @1;continue; }
[1]
anysym1: /[^*]+/		{ continue; }
anysym1: /*/
'*/':    /\*\//         { group = @0;continue; }

# Attributes

[]

# Main rule

input ::
  = using_directivesopt namespace_member_declarationsopt
  = using_directivesopt attributes
;

# Modifiers

modifier ::
  = Lnew
  = Lpublic
  = Lprotected
  = Linternal
  = Lprivate
  = Labstract
  = Lsealed
  = Lstatic
  = Lreadonly
  = Lvirtual
  = Loverride
  = Lextern
  = Lvolatile
  = Lunsafe
;

modifiers ::
  = modifier
  = modifiers modifier
;

optsemicolon ::= | ';' ;

# A.1.8 Literals

literal (Node) ::
  = Licon												{ $$ = new Node(); break; }
  = Lfcon                                               { $$ = new Node(); break; }
  = Lccon                                               { $$ = new Node(); break; }
  = Lscon                                               { $$ = new Node(); break; }
  = Ltrue                                               { $$ = new Node(); break; }
  = Lfalse                                              { $$ = new Node(); break; }
  = Lnull                                               { $$ = new Node(); break; }
;

# A.2 Syntactic grammar

# A.2.1 Basic concepts

qualified_identifier ::
  = identifier
  = qualified_identifier '.' identifier
;

ID (Node) ::
  = identifier											{ $$ = new Node( Kind.Identifier, null, null, null ); break; }
;


# A.2.2 Types

type_name ::
  = qualified_identifier
;

type ::
  = type_name
  = builtin_types
  = array_type
  = pointer_type
;

non_array_type ::
  = type_name
  = builtin_types
  = pointer_type
;

local_type ::
  = primary_expression
  = builtin_types
  = builtin_types rank_specifiers
;

cast_type ::
  = builtin_types
  = cast_type '*'
  = cast_type rank_specifier
;

builtin_types ::
  = Lobject
  = Lstring
  = Lbool
  = Ldecimal
  = Lfloat
  = Ldouble
  = integral_type
  = Lvoid
;

integral_type ::
  = Lsbyte
  = Lbyte
  = Lshort
  = Lushort
  = Lint
  = Luint
  = Llong
  = Lulong
  = Lchar
;

pointer_type ::
  = type '*'
;

# A.2.4 Expressions

argument_list ::
  = argument
  = argument_list ',' argument
;

argument ::
  = expression
  = Lref expression
  = Lout expression
;

primary_expression (Node) ::
  = primary_expression_na
  = new_array_expression
;

primary_expression_na (Node) ::
  = literal
  = ID
  = parenthesized
  = Lthis												{ $$ = new Node(); break; }
  = Lbase '.' ID										{ $$ = new Node(); break; }
  = Lbase '[' expression_list ']'                       { $$ = new Node(); break; }
  = Ltypeof '(' type ')'                                { $$ = new Node(); break; }
  = Lchecked '(' expression ')'                         { $$ = new Node(); break; }
  = Lunchecked '(' expression ')'                       { $$ = new Node(); break; }
  = Lsizeof '(' type ')'                                { $$ = new Node(); break; }
  = primary_expression '++'                             { $$ = new Node(); break; }
  = primary_expression '--'                             { $$ = new Node(); break; }
  = primary_expression '->' ID                          { $$ = new Node(); break; }
  = primary_expression '.' ID							{ $$ = new Node( Kind.Dot, $primary_expression, null, null ); break; }
  = builtin_types '.' ID								{ $$ = new Node( Kind.TypeDot, null, null, null ); break; }
  = primary_expression '(' argument_listopt ')'         { $$ = new Node(); break; }
  = primary_expression_na '[' expression_list ']'       { $$ = new Node(); break; }
  = primary_expression_na rank_specifier				{ $$ = new Node( Kind.DimmedExpr, null, null, $primary_expression_na ); break; }
  = object_or_delegate_creation_expression				{ $$ = new Node(); break; }
;

new_array_expression (Node) ::
  = Lnew non_array_type '[' expression_list ']' rank_specifiersopt array_initializeropt		{ $$ = new Node(); break; }
  = Lnew array_type array_initializer                   { $$ = new Node(); break; }
;

object_or_delegate_creation_expression (Node) ::=
	Lnew type '(' argument_listopt ')'                  { $$ = new Node(); break; }
;

unary_expression (Node) ::
  = primary_expression
  = '+' unary_expression								{ $$ = new Node(); break; }
  = '-' unary_expression								{ $$ = new Node(); break; }
  = '!' unary_expression                                { $$ = new Node(); break; }
  = '~' unary_expression                                { $$ = new Node(); break; }
  = '++' unary_expression                               { $$ = new Node(); break; }
  = '--' unary_expression                               { $$ = new Node(); break; }
  = cast_expression
;

parenthesized (Node) ::
  = '(' expression ')'
		$$ = $1;
		// HACK to resolve Cast ambiguity
		if( CastExpr($expression,lapg_n) ) {
			// perform reduce
			for( int e = lapg_rlen[lapg_i]; e > 0; e-- ) lapg_m[lapg_head--].sym = null;
			lapg_m[++lapg_head] = lapg_gg;
			lapg_m[lapg_head].state = lapg_state_sym( lapg_m[lapg_head-1].state, lapg_gg.lexem );
			if( lapg_m[lapg_head].state != -1 ) {
				// shift cast_token (if possible)
				lapg_gg.lexem = (int)Tokens.cast_token;
				lapg_gg.sym = null;
				lapg_gg.state = lapg_state_sym( lapg_m[lapg_head].state, lapg_gg.lexem );
				lapg_gg.pos = lapg_gg.endpos = lapg_n.pos;
				if( lapg_gg.state != -1 )
					lapg_m[++lapg_head] = lapg_gg;
			}
			// skip default reduce code
			continue;
		}
		break;
;

cast_expression (Node) ::
  = parenthesized cast_token unary_expression                   { $$ = new Node(); break; }
  = '(' cast_type ')' unary_expression                          { $$ = new Node(); break; }
;

multiplicative_expression (Node) ::
  = unary_expression
  = multiplicative_expression '*' unary_expression              { $$ = new Node(); break; }
  = multiplicative_expression '/' unary_expression              { $$ = new Node(); break; }
  = multiplicative_expression '%' unary_expression              { $$ = new Node(); break; }
;

additive_expression (Node) ::
  = multiplicative_expression
  = additive_expression '+' multiplicative_expression           { $$ = new Node(); break; }
  = additive_expression '-' multiplicative_expression           { $$ = new Node(); break; }
;

shift_expression (Node) ::
  = additive_expression
  = shift_expression '<<' additive_expression                   { $$ = new Node(); break; }
  = shift_expression '>>' additive_expression					{ $$ = new Node(); break; }
;

relational_expression (Node) ::
  = shift_expression
  = relational_expression '<' shift_expression                  { $$ = new Node(); break; }
  = relational_expression '>' shift_expression                  { $$ = new Node(); break; }
  = relational_expression '<=' shift_expression                 { $$ = new Node(); break; }
  = relational_expression '>=' shift_expression                 { $$ = new Node(); break; }
  = relational_expression Lis type                              { $$ = new Node(); break; }
  = relational_expression Las type                              { $$ = new Node(); break; }
;

equality_expression (Node) ::
  = relational_expression
  = equality_expression '==' relational_expression              { $$ = new Node(); break; }
  = equality_expression '!=' relational_expression              { $$ = new Node(); break; }
;

and_expression (Node) ::
  = equality_expression
  = and_expression '&' equality_expression                      { $$ = new Node(); break; }
;

exclusive_or_expression (Node) ::
  = and_expression
  = exclusive_or_expression '^' and_expression                  { $$ = new Node(); break; }
;

inclusive_or_expression (Node) ::
  = exclusive_or_expression
  = inclusive_or_expression '|' exclusive_or_expression         { $$ = new Node(); break; }
;

conditional_and_expression (Node) ::
  = inclusive_or_expression
  = conditional_and_expression '&&' inclusive_or_expression     { $$ = new Node(); break; }
;

conditional_or_expression (Node) ::
  = conditional_and_expression
  = conditional_or_expression '||' conditional_and_expression   { $$ = new Node(); break; }
;

conditional_expression (Node) ::
  = conditional_or_expression
  = conditional_or_expression '?' expression ':' expression		{ $$ = new Node(); break; }
;

assignment (Node) ::
  = unary_expression assignment_operator expression		{ $$ = new Node(); break; }
;

assignment_operator ::=
	  '='
	| '+='
	| '-='
	| '*='
	| '/='
	| '%='
	| '&='
	| '|='
	| '^='
	| '<<='
	| '>>='
;

expression_list ::
  = expression
  = expression_list ',' expression
;

expression (Node) ::
  = conditional_expression
  = assignment
;

constant_expression (Node) ::
  = expression
;

# A.2.5 Statements

statement ::
  = ID ':' statement
  = local_variable_declaration ';'
  = local_constant_declaration ';'
  = embedded_statement
;

embedded_statement ::
  = block
  = statement_expression ';'
  = ';'
  = Lif '(' expression ')' embedded_statement
  = Lif '(' expression ')' embedded_statement Lelse embedded_statement
  = Lswitch '(' expression ')' switch_block
  = Lwhile '(' expression ')' embedded_statement
  = Ldo embedded_statement Lwhile '(' expression ')' ';'
  = Lfor '(' for_initializeropt ';' expressionopt ';' statement_expression_listopt ')' embedded_statement
  = Lforeach '(' type ID Lin expression ')' embedded_statement
  = Lbreak ';'
  = Lcontinue ';'
  = Lgoto ID ';'
  = Lgoto Lcase constant_expression ';'
  = Lgoto Ldefault ';'
  = Lreturn expressionopt ';'
  = Lthrow expressionopt ';'
  = try_statement
  = Llock '(' expression ')' embedded_statement
  = Lusing '(' resource_acquisition ')' embedded_statement
  = Lunsafe block
  = Lchecked block
  = Lunchecked block
;

block ::
  = '{' statementsopt '}'
;

statements ::
  = statement
  = statements statement
;

local_variable_declaration ::
  = local_type variable_declarators
;

local_constant_declaration ::
  = Lconst type constant_declarators
;

constant_declarators ::
  = constant_declarator
  = constant_declarators ',' constant_declarator
;

constant_declarator ::
  = ID '=' constant_expression
;

statement_expression ::
  = primary_expression '(' argument_listopt ')'
  = object_or_delegate_creation_expression
  = assignment
  = primary_expression '++'
  = primary_expression '--'
  = '++' unary_expression
  = '--' unary_expression
;

switch_block ::
  = '{' switch_sectionsopt '}'
;

switch_sections ::
  = switch_section
  = switch_sections switch_section
;

switch_section ::
  = switch_labels statements_as_one
;

statements_as_one ::
  = statements
;

switch_labels ::
  = switch_label
  = switch_labels switch_label
;

switch_label ::
  = Lcase constant_expression ':'
  = Ldefault ':'
;

for_initializer ::
  = local_variable_declaration
  = statement_expression_list
;

statement_expression_list ::
  = statement_expression
  = statement_expression_list ',' statement_expression
;

try_statement ::
  = Ltry block catch_finally
;

catch_finally ::
  = catch_clauses
  = finally_clause
  = catch_clauses finally_clause
;

catch_clauses ::
  = specific_catch_clauses
  = specific_catch_clauses general_catch_clause
  = general_catch_clause
;

specific_catch_clauses ::
  = specific_catch_clause
  = specific_catch_clauses specific_catch_clause
;

specific_catch_clause ::
  = Lcatch '(' type_name IDopt ')' block

;

general_catch_clause ::
  = Lcatch block
;

finally_clause ::
  = Lfinally block
;

resource_acquisition ::
  = local_variable_declaration
  = expression
;

# Declaration level

# Namespaces

namespace_declaration ::
  = Lnamespace qualified_identifier '{' using_directivesopt namespace_member_declarationsopt '}' optsemicolon
;

using_directives ::
  = using_directive
  = using_directives using_directive
;

using_directive ::
  = Lusing ID '=' qualified_identifier ';'
  = Lusing qualified_identifier ';'
;

namespace_member_declarations ::
  = namespace_member_declaration
  = namespace_member_declarations namespace_member_declaration
;

namespace_member_declaration ::
  = namespace_declaration
  = type_declaration
;

type_declaration ::
  = class_declaration
  = struct_declaration
  = interface_declaration
  = enum_declaration
  = delegate_declaration
;

# A.2.6 Classes

class_declaration ::
  = attributesopt modifiersopt Lclass ID class_baseopt class_body optsemicolon
;

class_base ::
  = ':' type_list
;

type_list ::
  = qualified_identifier
  = type_list ',' qualified_identifier
;

class_body ::
  = '{' class_member_declarationsopt '}'
;

class_member_declarations ::
  = class_member_declaration
  = class_member_declarations class_member_declaration
;

class_member_declaration ::
  = constant_declaration
  = field_declaration
  = method_declaration
  = property_declaration
  = event_declaration
  = indexer_declaration
  = operator_declaration
  = constructor_declaration
  = destructor_declaration
  = type_declaration
;

# Constant/Field

constant_declaration ::
  = attributesopt const_modifiers type constant_declarators ';'
;

const_modifiers ::
  = Lconst
  = modifiers Lconst
;

field_declaration ::
  = attributesopt modifiersopt type variable_declarators ';'
;

variable_declarators ::
  = variable_declarator
  = variable_declarators ',' variable_declarator
;

variable_declarator ::
  = ID
  = ID '=' variable_initializer
;

variable_initializer ::
  = expression
  = array_initializer
;

# Method

method_declaration ::
  = attributesopt modifiersopt type member_name '(' formal_parameter_listopt ')' optional_body
;

member_name ::
  = identifier
  = qualified_identifier '.' identifier
;

formal_parameter_list ::
  = fixed_parameters
  = fixed_parameters ',' parameter_array
  = parameter_array
;

fixed_parameters ::
  = fixed_parameter
  = fixed_parameters ',' fixed_parameter
;

fixed_parameter ::
  = attributesopt parameter_modifieropt type ID
;

parameter_modifier ::
  = Lref
  = Lout
;

params_modifier ::
  = Lparams
;

parameter_array ::
  = attributesopt params_modifier array_type ID
;

# Property/Event/Indexer

property_declaration ::
  = attributesopt modifiersopt type member_name '{' accessor_declarations '}'
;

accessor_declarations ::
  = accessor_declaration accessor_declarationopt
;

accessor_declaration ::
  = attributesopt ID optional_body
;

optional_body ::
  = block
  = ';'
;

event_declaration ::
  = attributesopt modifiersopt Levent type variable_declarators ';'
  = attributesopt modifiersopt Levent type member_name '{' accessor_declarations '}'
;

indexer_declaration ::
  = attributesopt modifiersopt type indexer_name '[' formal_parameter_list ']' '{' accessor_declarations '}'
;

indexer_name ::
  = Lthis
  = qualified_identifier '.' Lthis
;

# Operators

operator_declaration ::
  = attributesopt modifiersopt operator_declarator optional_body
;

operator_declarator ::
  = unary_binary_operator_declarator
  = conversion_operator_declarator
;

simple_parameter ::
  = type ID
;

unary_binary_operator_declarator ::
  = type overloadable_operator_name '(' simple_parameter ')'
  = type overloadable_operator_name '(' simple_parameter ',' simple_parameter ')'
;

overloadable_operator_name ::=
	Loperator overloadable_operator						 ;

overloadable_operator ::=
	'+' | '-' |
	'!' | '~' | '++' | '--' | Ltrue | Lfalse |
	'*' | '/' | '%' | '&' | '|' | '^' | '<<' | '>>' | '==' | '!=' | '>' | '<' | '>=' | '<=' ;

conversion_operator_declarator ::
  = conversion_modifier Loperator type '(' simple_parameter ')'
;

conversion_modifier ::
  = Limplicit
  = Lexplicit
;

# Constructor/Destructor

constructor_declaration ::
  = attributesopt modifiersopt ID '(' formal_parameter_listopt ')' constructor_initializeropt optional_body
;

constructor_initializer ::
  = ':' Lbase '(' argument_listopt ')'
  = ':' Lthis '(' argument_listopt ')'
;

destructor_modifier ::
  = Lextern
;

destructor_declaration ::
  = attributesopt destructor_modifieropt '~' ID '(' ')' optional_body
;

# A.2.7 Structs

struct_declaration ::
  = attributesopt modifiersopt Lstruct ID struct_interfacesopt struct_body optsemicolon
;

struct_interfaces ::
  = ':' type_list
;

struct_body ::
  = '{' class_member_declarationsopt '}'
;

# A.2.8 Arrays

array_type ::=
	non_array_type rank_specifiers
;

rank_specifiers ::
  = rank_specifier
  = rank_specifiers rank_specifier
;

rank_specifier ::=
	'[' dim_separatorsopt ']' ;

dim_separators ::
  = ','
  = dim_separators ','
;

array_initializer ::
  = '{' variable_initializer_listopt '}'
  = '{' variable_initializer_list ',' '}'
;

variable_initializer_list ::
  = variable_initializer
  = variable_initializer_list ',' variable_initializer
;

# A.2.9 Interfaces

interface_declaration ::
  = attributesopt modifiersopt Linterface ID interface_baseopt interface_body optsemicolon
;

interface_base ::
  = ':' type_list
;

interface_body ::
  = '{' interface_member_declarationsopt '}'
;

interface_member_declarations ::
  = interface_member_declaration
  = interface_member_declarations interface_member_declaration
;

interface_member_declaration ::
  = attributesopt iface_member_modifieropt type ID '(' formal_parameter_listopt ')' ';'
  = attributesopt iface_member_modifieropt type ID '{' interface_accessors '}'
  = attributesopt iface_member_modifieropt Levent type ID ';'
  = attributesopt iface_member_modifieropt type iface_indexer_name '[' formal_parameter_list ']' '{' interface_accessors '}'
;

iface_indexer_name ::
  = Lthis
;

iface_member_modifier ::
  = Lnew
;

interface_accessors ::
  = interface_accessor interface_accessoropt
;

interface_accessor ::
  = attributesopt ID ';'
;

# A.2.10 Enums

enum_declaration ::
  = attributesopt modifiersopt Lenum ID enum_baseopt enum_body optsemicolon
;

enum_base ::
  = ':' integral_type
;

enum_body ::
  = '{' enum_member_declarationsopt '}'
  = '{' enum_member_declarations ',' '}'
;

enum_member_declarations ::
  = enum_member_declaration
  = enum_member_declarations ',' enum_member_declaration
;

enum_member_declaration ::
  = attributesopt ID
  = attributesopt ID '=' constant_expression
;

# A.2.11 Delegates

delegate_declaration ::
  = attributesopt modifiersopt Ldelegate type ID '(' formal_parameter_listopt ')' ';'
;

# A.2.12 Attributes

attributes ::
  = attribute_sect
  = attributes attribute_sect
;

attribute_sect ::
  = '[' attribute_target ':' attribute_list ',' ']'
  = '[' attribute_target ':' attribute_list ']'
  = '[' attribute_list ',' ']'
  = '[' attribute_list ']'
;

attribute_target ::
  = identifier
  = Levent
  = Lreturn
;

attribute_list ::
  = attribute
  = attribute_list ',' attribute
;

attribute ::
  = attribute_name attribute_argumentsopt
;

attribute_name ::
  = qualified_identifier
;

attribute_arguments ::
  = '(' attribute_argument_listopt ')'
;

attribute_argument_list ::
  = expression
  = attribute_argument_list ',' expression
;

%%

//#define DEBUG_syntax

using System;
using System.Collections;
using System.IO;

%%

public enum Kind {
	Identifier, TypeDot, Dot, DimmedExpr, Other
}

public class Node {
	public Kind kind;
	public Node left;
	public Node right;
	public Node expr;

	public Node() {
		this.kind = Kind.Other;
	}

	public Node( Kind kind, Node left, Node right, Node expr ) {
		this.kind = kind;
		this.left = left;
		this.right = right;
		this.expr = expr;
	}
}

// 14.6.6 Cast expressions =======
// To resolve cast-expression ambiguities, the following rule exists: A sequence of one or
// more tokens (§9.4) enclosed in parentheses is considered the start of a cast-expression only
// if at least one of the following are true:
//	• The sequence of tokens is correct grammar for a type, but not for an expression.
//  • The sequence of tokens is correct grammar for a type, and the token immediately following
//	  the closing parentheses is the token “~”, the token “!”, the token “(”, an identifier (§9.4.1),
//	  a literal (§9.4.4), or any keyword (§9.4.3) except as and is.

private static bool CastExpr( Node expr, lapg_symbol next ) {

	// check if expression can be type
	if( !CanBeType( expr ) )
		return false;

	// if expression is only type (contains dim_specifier)
	if( ContainsDimSpec( expr ) )
		return true;

	// Type or Expr
	switch( (Tokens)next.lexem ) {
		case Tokens.TILDE:
		case Tokens.EXCL:
		case Tokens.LROUNDBRACKET:
		case Tokens.identifier:
		case Tokens.Licon:
		case Tokens.Lfcon:
		case Tokens.Lscon:
		case Tokens.Lccon:
		case Tokens.Lfalse:
		case Tokens.Ltrue:
		case Tokens.Lnull:
			return true;
		case Tokens.Lis:
		case Tokens.Las:
			return false;
	}

	// keyword
	if( next.lexem >= (int)Tokens.Labstract && next.lexem <= (int)Tokens.Lwhile )
		return true;

	return false;
}

private static bool CanBeType( Node expr ) {
	switch( expr.kind ) {
		case Kind.Identifier: case Kind.TypeDot:
			return true;
		case Kind.Dot: 
			return CanBeType( expr.left );
		case Kind.DimmedExpr:
			return CanBeType( expr.expr );
	}
	return false;
}

private static bool ContainsDimSpec( Node expr ) {
	return expr.kind == Kind.DimmedExpr;
}

byte[] buff = new byte[1025];
int l, end;
BinaryReader r;

void fillb() {
	l = 0;
	end = r.Read( buff, 0, 1024 );
	buff[end] = 0;
}

void error( string s ) {
	System.Console.WriteLine(s);
}

public static void Main(string[] args) {
	parser p = new parser();
	
	if( args.Length > 0 ) 
		p.r = new BinaryReader( new FileStream(args[0],FileMode.Open) );
	else 
		p.r = new BinaryReader( System.Console.OpenStandardInput() );

	p.fillb();
	p.parse();
}
